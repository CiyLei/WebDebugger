apply plugin: 'com.novoda.bintray-release'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.protobuf'

publish {
    userOrg = 'ciy'//bintray用户名
    repoName = 'android'//要传到的maven的名字。你可能有多个maven，要传哪个写哪个。
    groupId = 'com.ciy'//jcenter上的路径
    artifactId = 'WebDebugger'//项目名称
    publishVersion = '1.0.0'//版本号
    desc = 'android debug'//描述，不重要
    website = 'https://github.com/CiyLei/WebDebugger'//网站，不重要；尽量模拟github上的地址
}

android {
    compileSdkVersion 28

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
        resValue("string", "HTTP_PORT", "8080")
        resValue("string", "WEB_SOCKET_PORT", "8081")
        resValue("string", "RESOURCE_PORT", "8082")
        resValue("string", "SERVICEHOST", "\"192.168.2.116\"")
        resValue("string", "SERVICEPORT", "8083")
        resValue("string", "PORT_NUMBER", "7999")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            proto {
                srcDir 'src/main/proto'
            }
        }
    }

}

protobuf {
    //配置protoc编译器
    protoc {
        artifact = 'com.google.protobuf:protoc:3.6.1'
    }
    //这里配置生成目录，编译后会在build的目录下生成对应的java文件
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.builtins {
                java {}
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    api 'org.nanohttpd:nanohttpd:2.3.1'
    api 'org.nanohttpd:nanohttpd-websocket:2.3.1'
    api 'org.nanohttpd:nanohttpd-webserver:2.3.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    compileOnly 'com.android.support:support-compat:28.0.0'
    implementation 'com.googlecode.mp4parser:isoparser:1.1.22'
    implementation 'com.tencent.mars:mars-core:1.2.3'
    implementation 'com.google.protobuf:protobuf-java:3.6.1'
    implementation ('android.arch.persistence.room:runtime:1.1.1', {
        exclude group: 'com.android.support'
    })
    kapt 'android.arch.persistence.room:compiler:1.1.1'
    implementation ('com.amitshekhar.android:debug-db:1.0.6', {
        exclude group: 'com.android.support'
    })
    implementation 'com.github.smarxpan:NotchScreenTool:0.0.1'
}
